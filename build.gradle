import java.nio.file.Paths

group 'hashcode.bali'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
}

def npmToolsDir = '../hash-code-2018-training'

def inputFiles = new File("${buildDir}/resources/main/input/").listFiles().findAll{it.isFile() && it.name.endsWith(".txt")}
inputFiles.each {
    input ->
        def fichierSansExtension = input.name.split("\\.")[0]
        tasks.create(name: "generate-$fichierSansExtension", type: BaliTask, group: 'hashcode') {
            inputFile input
            outputFile "${buildDir}/libs/${fichierSansExtension}.out.txt"
            classpath sourceSets.main.runtimeClasspath
            main = "hashcode.Main"
        }
}

class BaliTask extends JavaExec {
    def inputFile
    def outputFile
    @TaskAction
    void exec() {
        print "Input : ${inputFile}\n"
        standardInput = new FileInputStream(inputFile)
        standardOutput = new FileOutputStream(outputFile)
        super.exec()
        print "Output : ${outputFile}\n"
    }
    @InputFile
    def getInputFile() {
        return inputFile
    }

    @OutputFile
    def getOutputFile() {
        return Paths.get(outputFile).toFile()
    }
}

task generateAll (group: 'hashcode') {
    inputFiles.each {
        dependsOn "generate-${it.name.split("\\.")[0]}"
    }
    dependsOn 'sourcesJar'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task download (type: Exec, group: 'hashcode') {
    def cmd = commandExec('npm run download --prefix ../hash-code-2018-training/')
    commandLine cmd
}

task submit (type: Exec, group: 'hashcode') {
    inputs.files(fileTree("${buildDir}/libs/"))
    outputs.files(fileTree("${buildDir}/libs/"))
    dependsOn 'generateAll'
    def cmd = commandExec('npm run upload --prefix ../hash-code-2018-training/')
    commandLine cmd
}

task gitCloneHugoTool(type: Exec, group: 'hashcode init env') {
    def cmd = commandExec('git clone https://github.com/fnegre/hash-code-2018-training/ ../hash-code-2018-training')
    commandLine cmd
}

task npmInstallHugoTool(type: Exec, group: 'hashcode init env') {
    def cmd = commandExec('npm install --prefix ../hash-code-2018-training/')
    commandLine cmd
}

task createEnvFIle(group: 'hashcode init env') {
    new File("${npmToolsDir}/.env").write(
"""HASH_CODE_JUDGE_AUTH_TOKEN=ya29.Gl1wBXcTHKwTJlr0tIijmbI_pNCrXZsMMy_clmuUdZ2zkMTGHVoLcGb2QXCxVNdRMrg_70m9fKCT-6hYhpJwdzOh6wi-olpbcI_Lt6mgkXIWEgQVyDejsUnZZCp-Mw8f
DOWNLOAD_DIR=../bali-hashcode/src/main/resources/input/
BUILD_DIR=../bali-hashcode/build/libs/
GIT_TAG_ENABLED=false
"""
    )
}

def commandExec(def cmd) {
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        cmd =  'cmd /C '.concat(cmd)
    }
    cmd.split(' ')
}
